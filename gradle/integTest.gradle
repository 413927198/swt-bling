allprojects { Project proj ->
  proj.plugins.withType(org.gradle.api.plugins.JavaPlugin) {

    proj.sourceSets {
      integTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
      }
    }

    proj.configurations {
      integTestCompile.extendsFrom testCompile
      integTestRuntime.extendsFrom testRuntime
      junitAnt
    }

    proj.dependencies {
      junitAnt('org.apache.ant:ant-junit:1.8.4') {
        transitive = false
      }
    }

    task integTest(type: Test, group: 'Verification') {

      ant.taskdef(name: 'junit', classname: 'org.apache.tools.ant.taskdefs.optional.junit.JUnitTask',
              classpath: configurations.junitAnt.asPath)
      doLast {
        ant.junit(printsummary: true, showoutput: true, fork: true, forkmode: "once", clonevm: false, haltonerror: true, haltonfailure: true) {
          if (platform == 'darwin') {
            jvmarg(value: '-XstartOnFirstThread')
          }
          formatter(type: 'plain', usefile: 'false')
          batchtest {
            fileset(dir: 'build/classes/integTest') {
              include(name: '**/*IntegTest.class')
            }
          }
          sourceSets.integTest.runtimeClasspath.collect { relativePath(it) }.sort().each { classpath(path: it) }
        }
      }
    }

    proj.integTest {
      dependsOn compileIntegTestJava
    }

    proj.plugins.withType(org.gradle.plugins.ide.idea.IdeaPlugin) {
      proj.afterEvaluate {
        idea {
          module {
            testSourceDirs += sourceSets.integTest.java.srcDirs
            scopes.TEST.plus.add(configurations.integTestRuntime)
            scopes.TEST.plus.add(configurations.integTestCompile)
          }
        }
      }
    }

    proj.plugins.withType(org.gradle.plugins.ide.eclipse.EclipsePlugin) {
      eclipse {
        classpath {
          plusConfigurations.add(configurations.integTestCompile)
          plusConfigurations.add(configurations.integTestRuntime)
        }
      }
    }
  }
}