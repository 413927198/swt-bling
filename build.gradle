// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'findbugs'
apply from: 'gradle/swt.gradle'
apply from: 'gradle/integTest.gradle'
apply from: 'gradle/example.gradle'

defaultTasks 'build'

public String getPlatform(String platform) {
  switch (platform.replaceAll(' ', '').toLowerCase()) {
    case ~/.*linux.*/:  return 'linux'
    case ~/.*darwin.*/: return 'darwin'
    case ~/.*osx.*/:    return 'darwin'
    case ~/.*win.*/:    return 'windows'
    default:            return null
  }
}

ext {
  swtVersion = '4.3'
  swtArtifactName = "org.eclipse.swt.${swtWindowingLibrary}.${swtPlatform}.${swtArch}"

  platform = getPlatform(System.properties['os.name'])
}

repositories {
  mavenCentral()
  maven {
    url "https://swt-repo.googlecode.com/svn/repo/"
  }
}

dependencies {
  compile 'org.slf4j:slf4j-api:1.7.5'
  compile "org.eclipse.swt:${swtArtifactName}:${swtVersion}"
  testCompile "junit:junit:4.11"
  testCompile "org.mockito:mockito-all:1.9.5"
}

findbugs {
  reportLevel = "high"
}

tasks.withType(FindBugs) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}